## 테라폼 모듈(Module)

1. 테라폼 모듈(Module)이란?

가장 간단하게 비유하자면, 프로그래밍 언어의 함수(Function)와 같습니다.

모듈은 특정 목적을 위해 함께 사용되는 여러 리소스(Resource)들의 묶음을 담고 있는 컨테이너입니다. 예를 들어, "웹 서버 한 대를 구성한다"는 목적을 위해 필요한 EC2 인스턴스, 보안 그룹, 탄력적 IP 등을 하나의 모듈로 묶을 수 있습니다.

모듈을 사용하는 이유 (장점):

* 코드 재사용성 (Reusability): 똑같은 구성의 웹 서버를 여러 개 만들 때, 코드를 복사-붙여넣기 할 필요 없이 모듈을 여러 번 호출하여 간편하게 생성할 수 있습니다.
* 코드 정리 및 구조화 (Organization): 복잡한 인프라를 논리적인 단위로 나눌 수 있습니다. 예를 들어, 네트워크 모듈, 웹서버 모듈, 데이터베이스 모듈 등으로 분리하여 전체 구조를 이해하기 쉽게 만듭니다.
* 표준화 (Standardization): 조직 내에서 인프라를 구성하는 표준 방식을 모듈로 만들어 제공할 수 있습니다. 예를 들어, 보안 규칙이 강화된 EC2 인스턴스 모듈을 만들어 팀원들이 사용하게 함으로써 일관된 정책을 유지할 수 있습니다.
* 추상화 (Abstraction): 모듈 사용자는 모듈 내부의 복잡한 리소스 구성을 알 필요 없이, 필요한 입력값(예: 인스턴스 타입, 이름)만 전달하여 원하는 인프라를 구축할 수 있습니다.

---
전체 폴더 구조 
```
step16
├── modules
│   └── step16
│       ├── main.tf
│       ├── outputs.tf
│       └── variables.tf
├── main.tf
├── outputs.tf
├── provider.tf
├── terraform.tfvars
└── variables.tf
```

2. 모듈 선언 방법 (모듈 만들기)

모듈은 별도의 디렉터리에 .tf 파일들로 구성됩니다. 일반적으로 다음과 같은 파일 구조를 가집니다.
```
  /modules
    └─proxmox_vm
        ├── main.tf        # 모듈의 핵심 로직, 실제 리소스들을 정의
        ├── outputs.tf     # 모듈이 호출될 때 받을 입력 변수들을 정의
        └── variables.tf   # 모듈이 생성한 리소스의 정보를 외부로 내보낼 출력값들을 정의
``
예제: proxmox_vm 모듈 만들기

  1. `modules/proxmox_vm/variables.tf` (입력 정의)

  모듈이 외부로부터 받을 파라미터를 정의합니다.
  외부에서 받을 파라미터는 vm_name, proxmox_node, template_name, ciValue 입니다 

```
vi modules/proxmox_vm/variables.tf

# VM 생성을 위해 필요한 변수
variable "vm_name" {
  type        = string
  description = "생성할 가상머신의 이름"
}

variable "proxmox_node" {
  type        = string
  description = "VM을 생성할 대상 Proxmox 노드의 이름"
}

variable "template_name" {
  type        = string
  description = "복제에 사용할 템플릿의 이름"
}

# cloud-init 및 ssh 공개키 설정시 사용될 변수를 Object 타입으로 선언
variable ciValue {
  type = object({
    user = string     # "Cloud-Init 사용자 이름"
    password = string # "Cloud-Init 사용자 암호"
    ssh_key = string  # "VM에 주입할 사용자의 SSH 공개 키"
  })
  description = "cloud-init 및 ssh 공개키 설정시 사용될 변수"
}

```


2. `modules/proxmox_vm/main.tf` (핵심 로직)
```
terraform {
  required_providers {
    proxmox = {
      source  = "telmate/proxmox"
      version = "3.0.2-rc06"
    }
  }
}

# Proxmox VM 리소스 정의
resource "proxmox_vm_qemu" "ubuntu_vm" {
  # VM의 이름 설정
  name        = var.vm_name
  # VM이 배포될 Proxmox 노드의 이름
  target_node = var.proxmox_node

  # 복제할 템플릿의 이름
  clone       = var.template_name
  # VM 생성 시 풀 클론(독립적인 복사본)을 수행할지 여부
  full_clone  = true

  # 운영체제 타입 설정 (Cloud-Init을 사용)
  os_type = "cloud-init"
  # QEMU Guest Agent 활성화 (VM과 호스트 간 통신)
  agent   = 1

  # CPU 설정
  cpu {
    # 할당할 CPU 코어 수
    cores = 1
    # 할당할 CPU 소켓 수
    sockets = 1
  }

  # 할당할 메모리 양 (MB 단위)
  memory  = 1024
  # 메모리 벌루닝 활성화 (최소 512MB 보장)
  balloon = 512
  # SCSI 컨트롤러 하드웨어 타입 (성능 향상을 위해 VirtIO SCSI 권장)
  scsihw  = "virtio-scsi-pci"
  # 부팅 순서 (scsi0 디스크로 부팅)
  boot    = "order=scsi0"

  # 메인 디스크 설정 (루트 디스크)
  disk {
    # 디스크 슬롯 (scsi0)
    slot    = "scsi0"
    # 디스크 크기 (8GB)
    size    = "8G"
    # 디스크 종류 (일반 디스크)
    type    = "disk"
    # 디스크 저장소 (예: local-lvm)
    storage = "local-lvm"
  }

  # Cloud-Init 드라이브 설정
  disk {
    # 디스크 슬롯 (ide2는 일반적으로 Cloud-Init CD-ROM 드라이브에 사용)
    slot    = "ide2"
    # 디스크 종류 (Cloud-Init 드라이브)
    type    = "cloudinit"
    # 디스크 저장소
    storage = "local-lvm"
  }


  # 네트워크 인터페이스 설정
  network {
    # 네트워크 인터페이스 ID (net0)
    id     = 0
    # 네트워크 카드 모델 (VirtIO는 성능이 좋음)
    model  = "virtio"
    # 연결할 가상 브릿지
    bridge = "vmbr0"
  }

  # Cloud-Init을 통한 IP 설정 (DHCP로 IP 자동 할당)
  ipconfig0 = "ip=dhcp"

  # Cloud-Init 업그레이드 활성화
  ciupgrade  = true
  
  # Cloud-Init 사용자 이름과 비밀번호 및 ssh 키 설정
  ciuser     = var.ciValue.user
  cipassword = var.ciValue.password
  sshkeys    = var.ciValue.ssh_key

  # 사용자 정의 Cloud-Init 설정 적용
  # 위에서 생성한 스니펫 파일을 'vendor' 데이터로 이 VM에 적용합니다.
  cicustom = "vendor=local:snippets/cloud_init.yml"

  # VGA 설정
  vga {
    # VGA 타입 (표준)
    type = "std"
  }

  # Terraform 라이프사이클 관리 설정
  lifecycle {
    # 특정 속성의 변경을 Terraform이 무시하도록 설정 (수동 변경 시 유용)
    ignore_changes = [
      network, # 네트워크 설정 변경 무시
      disk     # 디스크 설정 변경 무시
    ]
  }
}

```

3. `modules/proxmox_vm/outputs.tf` (출력 정의)

모듈을 호출한 곳에서 사용할 수 있도록, 생성된 리소스의 속성을 출력값으로 정의합니다.

```
vi modules/proxmox_vm/outputs.tf

output "server_ip" {
  description = "서버의 기본 IPv4 주소입니다."
  value = proxmox_vm_qemu.ubuntu_vm.default_ipv4_address
}
```

---

4. 모듈 사용 방법 (모듈 호출하기)

  이제 위에서 만든 모듈을 루트(root) Terraform 구성 파일에서 module 블록을 사용해 호출할 수 있습니다.

  예제: main.tf에서 proxmox_vm 모듈 사용하기
```
vi main.tf

module "proxmox_server" {
  source = "./modules/proxmox_vm"

  vm_name       = var.vm_name
  proxmox_node  = var.proxmox_node
  template_name = var.template_name
  ciValue       = var.ciValue
}
```

### 실행 방법:
* terraform apply를 실행하면 vms.auto.tfvars 파일에 정의된 사양대로 웹 서버 2대와 DB 서버 1대가 각각 다른 이름과 사양으로 생성됩니다.

```bash
terraform init
terraform plan
terraform apply -auto-approve
```

생성된 가상 머신에 접속해 봅니다
```
userid : kosa
password : kosa1004
```
입력하면 로그인이 되는 것을 확인할 수 있습니다

* terraform apply의 실행결과로 server_ip을 확인해봅니다.
* 생성된 가상머신에 로그인하여 ip a 명령으로 ip을 확인합니다.
* server_ip와 ip a 명령의 결과 같은지 확인합니다.

* ip의 주소(예 : 192.168.0.4)으로 ssh로 접속할 때 공개키로 접속이 되는 것을 확인할 수 있습니다
