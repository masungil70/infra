## 변수 선언 variables.tf 파일 분리 및 설명과 기본값 설정 

폴더 구조

step4
├── step4.tf          # 변수 사용
├── terraform.tfvars  # 변수에 값 설정
└── variables.tf      # 변수 선언

변수를 선언합니다 
```bash
vi variables.tf 

# Proxmox 프로바이더 연결에 필요한 변수 선언
# 선언된 변수에 값을 설정하는 것은 terraform.tfvars 파일에서 진행합니다.

variable "proxmox_api_url" {
  type        = string
  description = "Proxmox API의 전체 URL 주소 (예: https://1.2.3.4:8006/api2/json)"
  sensitive   = true
}

variable "proxmox_api_token_id" {
  type        = string
  description = "Proxmox API 토큰 ID (예: user@pve!token-name)"
  sensitive   = true
}

variable "proxmox_api_token_secret" {
  type        = string
  description = "Proxmox API 토큰 Secret"
  sensitive   = true
}

# VM 생성을 위해 필요한 변수
variable "vm_name" {
  type        = string
  description = "생성할 가상머신의 이름"
}

variable "proxmox_node" {
  type        = string
  description = "VM을 생성할 대상 Proxmox 노드의 이름"
}

variable "template_name" {
  type        = string
  description = "복제에 사용할 템플릿의 이름"
}

variable "ssh_public_key" {
  type        = string
  description = "VM에 주입할 사용자의 SSH 공개 키"
  sensitive   = true
}

variable "ciuser" {
  type        = string
  description = "Cloud-Init 사용자 이름"
}

variable "cipassword" {
  type        = string
  description = "Cloud-Init 사용자 암호"
  sensitive   = true
}
```

---

선언된 변수에 값을 설정 하고 해당 파일은 github에 push 하지 않습니다

```bash
vi terraform.tfvars

# Proxmox API 접속 정보
# [주의] 이 파일은 .gitignore에 추가하여 버전 관리에 포함되지 않도록 하세요.
proxmox_api_url          = "https://192.168.0.3:8006/api2/json"
proxmox_api_token_id     = "terraform-prov@pve!terraform-token"
proxmox_api_token_secret = "e3f31683-a95e-42c3-*****"

# 가상머신 구성 정보
vm_name       = "kosa-vm-01"
proxmox_node  = "masungil1"
template_name = "ubuntu-2204-template"
ssh_public_key = "ssh-rsa *******"

# Cloud-Init 정보
ciuser     = "kosa"
cipassword = "kosa1004"

```

---

```bash
vi step4.tf

# 사용할 프로바이더와 버전 설정
terraform {
  required_providers {
    proxmox = {
      # 참고: 이 코드는 telmate/proxmox 프로바이더 문법을 기준으로 합니다.
      source  = "telmate/proxmox"
      version = "3.0.2-rc06"
    }
  }
}

# Proxmox 프로바이더 구성
provider "proxmox" {
  pm_api_url          = var.proxmox_api_url
  pm_api_token_id     = var.proxmox_api_token_id
  pm_api_token_secret = var.proxmox_api_token_secret

  # 자체 서명 인증서 사용 시 필요
  pm_tls_insecure = true
}

# Proxmox VM 리소스 정의
resource "proxmox_vm_qemu" "ubuntu_vm" {
  # VM의 이름 설정
  name        = var.vm_name
  # VM이 배포될 Proxmox 노드의 이름
  target_node = var.proxmox_node
  # 복제할 템플릿의 이름
  clone       = var.template_name
  # VM 생성 시 풀 클론(독립적인 복사본)을 수행할지 여부
  full_clone  = true

  # 운영체제 타입 설정 (Cloud-Init을 사용)
  os_type = "cloud-init"
  # QEMU Guest Agent 활성화 (VM과 호스트 간 통신)
  agent   = 1

  # CPU 설정
  cpu {
    # 할당할 CPU 코어 수
    cores = 1
    # 할당할 CPU 소켓 수
    sockets = 1
  }

  # 할당할 메모리 양 (MB 단위)
  memory  = 1024
  # 메모리 벌루닝 활성화 (최소 512MB 보장)
  balloon = 512
  # SCSI 컨트롤러 하드웨어 타입 (성능 향상을 위해 VirtIO SCSI 권장)
  scsihw  = "virtio-scsi-pci"
  # 부팅 순서 (scsi0 디스크로 부팅)
  boot    = "order=scsi0"

  # 메인 디스크 설정 (루트 디스크)
  disk {
    # 디스크 슬롯 (scsi0)
    slot    = "scsi0"
    # 디스크 크기 (8GB)
    size    = "8G"
    # 디스크 종류 (일반 디스크)
    type    = "disk"
    # 디스크 저장소 (예: local-lvm)
    storage = "local-lvm"
  }

  # Cloud-Init 드라이브 설정
  disk {
    # 디스크 슬롯 (ide2는 일반적으로 Cloud-Init CD-ROM 드라이브에 사용)
    slot    = "ide2"
    # 디스크 종류 (Cloud-Init 드라이브)
    type    = "cloudinit"
    # 디스크 저장소
    storage = "local-lvm"
  }


  # 네트워크 인터페이스 설정
  network {
    # 네트워크 인터페이스 ID (net0)
    id     = 0
    # 네트워크 카드 모델 (VirtIO는 성능이 좋음)
    model  = "virtio"
    # 연결할 가상 브릿지
    bridge = "vmbr0"
  }

  # Cloud-Init을 통한 IP 설정 (DHCP로 IP 자동 할당)
  ipconfig0 = "ip=dhcp"

  # Cloud-Init 업그레이드 활성화
  ciupgrade  = true
  # Cloud-Init 사용자 이름
  ciuser     = var.ciuser
  # Cloud-Init 사용자 암호
  cipassword = var.cipassword

  # VGA 설정
  vga {
    # VGA 타입 (표준)
    type = "std"
  }

  # Terraform 라이프사이클 관리 설정
  lifecycle {
    # 특정 속성의 변경을 Terraform이 무시하도록 설정 (수동 변경 시 유용)
    ignore_changes = [
      network, # 네트워크 설정 변경 무시
      disk     # 디스크 설정 변경 무시
    ]
  }
}

```

#### 실행 방법:
```bash
terraform init
terraform plan
terraform apply -auto-approve
```


생성된 가상 머신에 접속해 봅니다
```
userid : kosa
password : kosa1004
```
입력하면 로그인이 되는 것을 확인할 수 있습니다


로그인 한 후 ip a로 ip의 주소를 확인하고 ssh로 접속할 때 인증서로 접속이 되는 것을 확인할 수 있습니다

주의 terraform 실행 시간의 2분이상 걸리는 이유

agent = 1이 설정되서 설치된 가상머신에 qemu-guest-agent가 설치되는 것을 확인 해야 terraform이 종료되는데 현재 template 이미지는 설치되지 않아 2분가 대기 하다 종료합니다

생성된 가상머신의 요약에 부분에 IP 주소가 표시되지 않습니다.

---
